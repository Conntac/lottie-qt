//
//  LOTRepeaterRenderer.m
//  Lottie
//
//  Created by brandon_withrow on 7/28/17.
//  Copyright Â© 2017 Airbnb. All rights reserved.
//

#import "LOTRepeaterRenderer.h"
#import "LOTTransformInterpolator.h"
#import "LOTNumberInterpolator.h"
#import "LOTHelpers.h"

#include <QSharedPointer>

LOTRepeaterRenderer::LOTRepeaterRenderer(const QSharedPointer<LOTAnimatorNode> &inputNode, LOTShapeRepeater *repeater)
: LOTRenderNode(inputNode, repeater.keyname)
{
    _transformInterpolator = _transformInterpolator.create(repeater.position.keyframes,
                                                           repeater.rotation.keyframes,
                                                           repeater.anchorPoint.keyframes,
                                                           repeater.scale.keyframes);
    _copiesInterpolator = _copiesInterpolator.create(repeater.copies.keyframes);
    _offsetInterpolator = _offsetInterpolator.create(repeater.offset.keyframes);
    _startOpacityInterpolator = _startOpacityInterpolator.create(repeater.startOpacity.keyframes);
    _endOpacityInterpolator = _endOpacityInterpolator.create(repeater.endOpacity.keyframes);

    _instanceLayer = [CALayer layer];
    recursivelyAddChildLayers(inputNode);

    _replicatorLayer = [CAReplicatorLayer layer];
    _replicatorLayer.actions = @{@"instanceCount" : [NSNull null],
                                 @"instanceTransform" : [NSNull null],
                                 @"instanceAlphaOffset" : [NSNull null]};
    [_replicatorLayer addSublayer:_instanceLayer];
    [outputLayer addSublayer:_replicatorLayer];

    centerPoint_DEBUG = [CALayer layer];
    centerPoint_DEBUG.bounds = CGRectMake(0, 0, 20, 20);
    if (ENABLE_DEBUG_SHAPES) {
      [outputLayer addSublayer:centerPoint_DEBUG];
    }
}


QMap<QString, QSharedPointer<LOTValueInterpolator> > LOTRepeaterRenderer::valueInterpolators() const
{
    QMap<QString, QSharedPointer<LOTValueInterpolator>> map;
    map.insert("Copies", _copiesInterpolator);
    map.insert("Offset", _offsetInterpolator);
    map.insert("Transform.Anchor Point", _transformInterpolator->anchorInterpolator);
    map.insert("Transform.Position", _transformInterpolator->positionInterpolator);
    map.insert("Transform.Scale", _transformInterpolator->scaleInterpolator);
    map.insert("Transform.Rotation", _transformInterpolator->rotationInterpolator);
    map.insert("Transform.Start Opacity", _startOpacityInterpolator);
    map.insert("Transform.End Opacity", _endOpacityInterpolator);
    return map;
}

bool LOTRepeaterRenderer::needsUpdateForFrame(qreal frame)
{
    // TODO BW Add offset ability
    return (_transformInterpolator->hasUpdateForFrame(frame) ||
            _copiesInterpolator->hasUpdateForFrame(frame) ||
            _startOpacityInterpolator->hasUpdateForFrame(frame) ||
            _endOpacityInterpolator->hasUpdateForFrame(frame));
}

void LOTRepeaterRenderer::performLocalUpdate()
{
    centerPoint_DEBUG.backgroundColor =  [UIColor greenColor].CGColor;
    centerPoint_DEBUG.borderColor = [UIColor lightGrayColor].CGColor;
    centerPoint_DEBUG.borderWidth = 2.f;

    CGFloat copies = ceilf(_copiesInterpolator->floatValueForFrame(currentFrame));
    _replicatorLayer.instanceCount = (NSInteger)copies;
    _replicatorLayer.instanceTransform = _transformInterpolator->transformForFrame(currentFrame);
    CGFloat startOpacity = _startOpacityInterpolator->floatValueForFrame(currentFrame);
    CGFloat endOpacity = _endOpacityInterpolator->floatValueForFrame(currentFrame);
    CGFloat opacityStep = (endOpacity - startOpacity) / copies;
    _instanceLayer.opacity = startOpacity;
    _replicatorLayer.instanceAlphaOffset = opacityStep;
}

void LOTRepeaterRenderer::recursivelyAddChildLayers(const QSharedPointer<LOTAnimatorNode> &node)
{
    QSharedPointer<LOTRenderNode> renderNode = node.dynamicCast<LOTRenderNode>();
    if (renderNode) {
      [_instanceLayer addSublayer:renderNode->outputLayer];
    }
    if (!node.dynamicCast<LOTRepeaterRenderer>() &&
        node->inputNode) {
      recursivelyAddChildLayers(node->inputNode);
    }
}
